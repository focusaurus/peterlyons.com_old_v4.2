.create_blog
  .notices
  section.preview
  hr
  label
    | title
    input(name="title")
  textarea.content(cols="80", rows="25")
  button Save
:coffeescript
  $find = (selector) =>
    $(".create_blog").find selector

  $preview = ->
    $find ".preview"

  content = ->
    $find(".content").val()

  preview = (content, $out)->
    $.ajax
      type: "POST"
      url: "/convert"
      contentType: "text/x-markdown"
      data: content
      success: (response) ->
        $out.html(response)

  handleKeys = (event) ->
    postDraft =
      content: event.target.value
      title: $find("[name=title]").val()
    localStorage.postDraft = JSON.stringify postDraft
    preview postDraft.content, $preview()

  save = ->
    data =
      title: $find("[name=title]").val()
      content: content()
    $.ajax
      type: "POST"
      url: "post"
      contentType: "application/json"
      data: JSON.stringify(data)
      success: (response) ->
        $(".notices").html("""New post saved at <a href='/#{response.URI}'>#{response.URI}</a>""")
        window.scrollTo 0, 0

  $ ->
    try
      savedPost = JSON.parse localStorage.postDraft
    catch error
      console.log localStorage.postDraft
      savedPost =
        title: ""
        content: ""
    $find(".content").val(savedPost.content)
      .focus().on "keyup", _.debounce handleKeys, 250
    $find("[name=title]").val(savedPost.title)
    preview savedPost.content, $preview()
    $find("button").on "click", save
