.create_blog
  .notices
  section.preview
  hr
  label
    | title
    input(name="title")
  textarea.content(cols="80", rows="25")
  label
    | password to save
    input(type="password")
  button Save
:coffeescript
  $find = (selector) =>
    $(".create_blog").find selector

  $preview = ->
    $find ".preview"

  content = ->
    $find(".content").val()

  preview = (content, $out)->
    $.ajax
      type: "POST"
      url: "/convert"
      contentType: "text/x-markdown"
      data: content
      success: (response) ->
        $out.html(response)

  handleKeys = (event) ->
    postDraft =
      content: event.target.value
      title: $find("[name=title]").val()
    localStorage.postDraft = JSON.stringify postDraft
    preview postDraft.content, $preview()

  save = ->
    $find(".notices").html("")
    data =
      title: $find("[name=title]").val()
      content: content()
      password: $find("[type=password]").val()
    $.ajax
      type: "POST"
      url: "post"
      contentType: "application/json"
      data: JSON.stringify(data)
      success: (response) ->
        $find(".notices").html("""New post saved at <a href='/#{response.URI}'>#{response.URI}</a>""")
        window.scrollTo 0, 0
      error: (response) ->
        $find(".notices").html("<strong>BORKED!</strong>")

  $ ->
    try
      savedPost = JSON.parse localStorage.postDraft
    catch error
      console.log localStorage.postDraft
      savedPost =
        title: ""
        content: ""
    $find(".content").val(savedPost.content)
      .focus().on "keyup", _.debounce handleKeys, 250
    $find("[name=title]").val(savedPost.title)
    preview savedPost.content, $preview()
    $find("button").on "click", save
